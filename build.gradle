buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
    }
}

plugins {
    id "org.sonarqube" version "1.2"
}

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/libraries.gradle"

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar-nprogramming.rhcloud.com"
        property "sonar.projectKey", "trading-office"
        property "sonar.projectName", "Trading Office"
    }
}

subprojects {
    repositories {
        mavenLocal()
        maven {
            url "http://repo1.maven.org/maven2"
            artifactUrls "https://repository.jboss.org/nexus/content/groups/public"
        }
        jcenter()
        maven { url "http://maven-repository.openspaces.org" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
    }

    apply plugin: 'java'
    apply plugin: 'idea'

    apply from: "$rootDir/gradle/jacoco.gradle"


    sourceCompatibility = 1.8

    test {
        testLogging {
            exceptionFormat = 'full'
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

defaultTasks 'clean', 'bootRepackage'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
